#@TYPE: Machine
#@NAME: Generic Kria KR260 Robotics Starter Kit
#@DESCRIPTION: Machine configuration for Kria KR260 Robotics Starter Kit (Kria K26 SOM + KR260 carrier board).

#### Preamble
MACHINEOVERRIDES =. "${@['', 'k26-smk-kr:']['k26-smk-kr' != '${MACHINE}']}"
MACHINEOVERRIDES =. "k26-kr:"
#### Regular settings follow

require conf/machine/include/k26-smk.inc
require conf/machine/include/kria-qemu-kr.inc

INITRAMFS_IMAGE = "petalinux-initramfs-image"

HDF_MACHINE = "k26-smk-kr"

UBOOT_DT_FILES = "\
    zynqmp-sck-kr-g-revA.dts \
    zynqmp-sck-kr-g-revB.dts \
    "

# DTB_FILE_NAME cant be changed globally or it will break the build but we can modify it for these
# tasks specifically so that the resulting symlink points to the correct file for a specific board
DTB_FILE_NAME:task-install:pn-device-tree = "SMK-zynqmp-sck-kr-g-revB.dtb"
DTB_FILE_NAME:task-deploy:pn-device-tree = "SMK-zynqmp-sck-kr-g-revB.dtb"
# Workaround for bitbake not detecting the above change in the hash
PR:append:pn-device-tree = ".1"

#### No additional settings should be after the Postamble
#### Postamble
PACKAGE_EXTRA_ARCHS:append = "${@['', ' k26_smk_kr']['k26-smk-kr' != "${MACHINE}"]}"
